plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.19'
    compile 'org.slf4j:slf4j-api:1.7.33'
    compile 'com.github.mwiede:jsch:0.2.4'

    testCompile('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy', module:'groovy-all'
    }
    testRuntime 'cglib:cglib-nodep:3.3.0'
    testRuntime 'org.objenesis:objenesis:3.2'

    testRuntime 'ch.qos.logback:logback-classic:1.4.4'
}

processResources {
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: ['version': project.version])
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

description = 'Groovy SSH library'
ext {
    license = 'Apache-2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    vcsUrl = 'https://github.com/int128/groovy-ssh.git'
}

publishing {
    publications {
        pluginIntegration(MavenPublication) {
            from components.java
            artifactId parent.name
        }
        release(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId parent.name
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name parent.name
                    description project.description
                    url project.vcsUrl
                    scm {
                        url project.vcsUrl
                        connection "scm:git:$project.vcsUrl"
                    }
                    licenses {
                        license {
                            name project.license
                            url project.licenseUrl
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id parent.group
                            name parent.group
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: findProperty('bintrayUser')
    key  = System.getenv('BINTRAY_KEY')  ?: findProperty('bintrayKey')
    publications = ['release']
    publish = true
    pkg {
        repo = 'maven'
        name = parent.name
        licenses = [project.license]
        vcsUrl = project.vcsUrl
        version {
            name = project.version
        }
    }
}
